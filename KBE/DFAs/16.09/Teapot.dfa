#! NX/KF 4.0 - Teapot
DefClass: Teapot (ug_base_part);
(number parameter) body_dia: 400; 					#tea pot body diameter
(number parameter) wall_thickness: 0.03*body_dia:; 	#tea pot wall thickness, default body_dia*0.03
(Point Parameter) center: point(18,100,-10); 		#center of tea pot body sphere
(number parameter) spout_length: 0.3*body_dia:; 	#spout length, default body_dia*0.3
(number parameter) handle_dia: 0.4*body_dia:;		#handle diameter, default body_dia*0.4
(integer parameter) color_body: 60;
(integer parameter) color_lid: 200;
(integer parameter) color_handle: 216;
(integer parameter) color_spout: 20;
(number) spout_length_min: body_dia:*0.2; 	#min spout length value, shorter than that will make the spout to small to act as a spout.
(number) spout_length_max: body_dia:*2;		#max spout length value, does not make sense to have a spout any longer.
(number) handle_dia_min: body_dia:*0.15; 	#min diameter value, less will make it immpossible to hold. 
(number) handle_dia_max: body_dia:*0.55;	#max diameter value, more will make the handle go beneath and on top of the teapot. 




#body sphere
(Child) the_body: {
		Class,		ug_sphere,
		Diameter,   body_dia:,
		Center,		center:,
    };

(Child) body_colored: { 
		Class, 		ug_body; 
		Feature, 	{the_body:};  
		Layer, 		1; 
		color, 		color_body:;
	};


#cut half of that body sphere - block or _cylinder_
(Child) the_body_cylinder_cut: {
        Class,        	ug_cylinder,
        Diameter,     	body_dia:,
		Origin,			center:;
		Direction, 		Vector(0, 0, -1);
		Height, 		body_dia:/2;
		Operation, 		Subtract;
		Target, 		{the_body:}
    };

#make a smaller sphere to represent internal space.
(Child) the_body_in: {
        Class,       	ug_sphere,
        Diameter,     	body_dia:-wall_thickness:*2,
		Center,			center:;
		Operation, 		Subtract;
		Target, 		{the_body:}
    };
	
#add a thin cylinder at the bottom and unite with the body
(Child) the_bottom_plate: {
        Class,      ug_cylinder;
        Diameter,   the_body:Diameter:;
		Origin,		center: + vector(0,0,-wall_thickness:);
		Height, 	body_dia:*0.03; #Same as wall_thickness
		Operation, 	unite;  
		Target, 	{the_body:};
    };


#Importing lid from Lid.dfa
(Child) the_lid: {
		Class,		Lid;
		body_dia, 	body_dia:; #tea pot body diameter
		center, 	center:;
		color, 		color_lid:;
	};

(Child) lid_colored: { 
		Class, 		ug_body; 
		Feature, 	{the_lid:the_lid:};  
		Layer, 		1; 
		color, 		color_lid:;
	};
  
# Make a body to be subtracted to make a lid cut
# Subtract that body.
(Child) the_lid_cut: {
        Class,		ug_sphere;
        Diameter,	the_lid:the_lid:Diameter:;
		Center, 	the_lid:the_lid:Center:;
		Operation, 	Subtract;
		Target, 	{the_body:};
	};

#Importing handle from Handle.dfa
(Child) the_handle: {
		Class,		Handle;
		body_dia, 	body_dia:; #tea pot body diameter
		handle_dia, handle_dia:; 
		center, 	center:;
	};

(Child) handle_colored: { 
		Class, 		ug_body; 
		Feature, 	{the_handle:the_handle_cylinder:};  
		Layer, 		1; 
		color, 		color_handle:;
	};


#Importing spout from Spout.dfa
(Child) the_spout: {
		Class,			Spout;
		body_dia, 		body_dia:; #tea pot body diameter
		wall_thickness, wall_thickness:; #tea pot wall thickness
		spout_length, 	spout_length:;
		center, 		center:;
		color, 			color_spout:;
	};

(Child) spout_colored: { 
		Class, 		ug_body; 
		Feature, 	{the_spout:the_spout_cone:};  
		Layer, 		1; 
		color, 		color_spout:;
	};
  


#Making cylinder matching the spout angle and diameter 
(Child) the_spout_hole: {
		Class,		ug_cylinder;
		Diameter,	the_spout:the_spout_cone:Base_Diameter:*0.8;
		Height, 	the_spout:spout_length:;
		Direction, 	the_spout:direction:;
		Origin, 	the_spout:the_spout_cone:Origin:;
	};

#Subtracting the cylinder from the main body to create hole for spout
(Child) the_hole_cut: {
		Class,		ug_boolean;
		Operation,  Subtract;
		Target,		{the_body:};
		Tool,		{the_spout_hole:};
	};


